// Top-level build file where you can add configuration options common to all sub-projects/modules.
// See http://rominirani.com/2014/07/29/gradle-tutorial-part-3-multiple-java-projects/
// See https://gist.github.com/gabrielemariotti/ad6672902464ee2392d0

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4' // Dagger 2 dependency
        classpath 'me.tatarka:gradle-retrolambda:3.1.0'            // Java8 lambda backport
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.1"
    }
    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    TARGET_SDK = 25
    BUILD_TOOLS = '25.0.2'
    SUPPORT_LIBRARY_VERSION = '25.3.0'
    PLAY_SERVICES_VERSION = '10.2.0'

    libs = [
        // Google
        appcompat: "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}",
        recyclerview: "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}",
        design: "com.android.support:design:${SUPPORT_LIBRARY_VERSION}",
        cardview: "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION",
        palette: "com.android.support:palette-v7:$SUPPORT_LIBRARY_VERSION",
        annotations: "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION",
        // Square
        picasso: 'com.squareup.picasso:picasso:2.5.2',
        butterknife: 'com.jakewharton:butterknife:8.5.1',
        okhttp: 'com.squareup.okhttp:okhttp:2.0.0',
        okhttp_urlconnection: 'com.squareup.okhttp:okhttp-urlconnection:2.0.0',
        okhttp3: 'com.squareup.okhttp3:okhttp:3.6.0',
        retrofit: 'com.squareup.retrofit:retrofit:1.9.0',
        retrofit2: 'com.squareup.retrofit2:retrofit:2.2.0',
        // Rx
        rxjava: 'io.reactivex:rxjava:1.2.9',
        rxandroid: 'io.reactivex:rxandroid:1.2.1',
        // DI
        dagger: 'com.google.dagger:dagger:2.10',
        dagger_compiler: 'com.google.dagger:dagger-compiler:2.10',
        javax_annotation: 'org.glassfish:javax.annotation:10.0-b28',
        // Kotlin
        kotlin: "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.1",
        // Tests
        junit: 'junit:junit:4.12',
    ]
}